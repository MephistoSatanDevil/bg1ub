IF
	Global("UBFightDezkiel","GLOBAL",1)
	Allegiance(Myself,NEUTRAL)
THEN
	RESPONSE #100
		Enemy()
		Continue()
END

IF
	AttackedBy([ANYONE],DEFAULT)
	Allegiance(Myself,NEUTRAL)
THEN
	RESPONSE #100
		SetGlobal("UBFightDezkiel","GLOBAL",1)
		ActionOverride("dezkiel",Enemy())
		Enemy()
		Continue()
END

IF
	Global("UBFightDezkiel","GLOBAL",0)
	Allegiance(Myself,NEUTRAL)
THEN
	RESPONSE #100
		NoAction()
END

IF
	Heard("dezkiel",314)
THEN
	RESPONSE #100
		Attack(LastTalkedToBy("dezkiel"))
END

IF
	Help("dezkiel")
THEN
	RESPONSE #100
		Attack(LastAttackerOf("dezkiel"))
END

IF
	Global("UBFightDezkiel","GLOBAL",1)
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("UBSNOWGOLEMBREATH","LOCALS")
	Range(NearestEnemyOf(Myself),10)
	RandomNum(4,1)
THEN
	RESPONSE #100
		SetGlobalTimer("UBSNOWGOLEMBREATH","LOCALS",30)
		ForceSpellRES("UBSNOBR",LastSeenBy(Myself)) // Snow Golem Breath
		Wait(2)
END

IF
	Global("UBFightDezkiel","GLOBAL",1)
	AttackedBy([ANYONE],DEFAULT)
	Range(LastAttackerOf(Myself),5)
THEN
	RESPONSE #100
		Attack(LastAttackerOf(Myself))
END

IF
	Global("UBFightDezkiel","GLOBAL",1)
	See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),5)
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),30)
END

IF
	Global("UBFightDezkiel","GLOBAL",1)
	AttackedBy([ANYONE],DEFAULT)
	!Range(LastAttackerOf(Myself),5)
THEN
	RESPONSE #100
		MoveToObject(LastAttackerOf(Myself))
END

IF
	Global("UBFightDezkiel","GLOBAL",1)
	See(NearestEnemyOf(Myself))
	!Range(LastSeenBy(Myself),5)
THEN
	RESPONSE #100
		MoveToObject(LastSeenBy(Myself))
END

IF
	Global("UBFightDezkiel","GLOBAL",1)
	!See(NearestEnemyOf(Myself))
	ActionListEmpty()
THEN
	RESPONSE #100
		ProtectObject("dezkiel",100)
END
